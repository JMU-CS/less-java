plugins {
    id 'java'
    id 'eclipse'
    id 'antlr'
    id 'application'
}

// Configure subprojects
subprojects {
    apply plugin: 'java'
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // JUnit
    compile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'

    // ANTLR v4
    antlr 'org.antlr:antlr4:4.5.3'
}

// Tasks
run {
    mainClassName = 'com.github.lessjava.LJCompiler'

    def defaultTestfile = 'tests/fact.lj'
    args = (project.hasProperty('testfile')) ? [testfile] : [defaultTestfile]
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.lessjava.LJCompiler'
    }
}

task fatJar(type:Jar) {
    manifest {
        attributes 'Main-Class': 'com.github.lessjava.LJCompiler'
    }
    baseName = 'lj-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

generateGrammarSource {
    arguments += ['-package', 'com.github.lessjava.generated']
    outputDirectory = file('build/generated-src/antlr/main/com/github/lessjava/generated')
}

