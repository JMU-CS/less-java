plugins {
    id 'java'
    id 'eclipse'
    id 'antlr'
    id 'application'
}

// Configure subprojects
subprojects {
    apply plugin: 'java'
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // JUnit
    compile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    compile 'org.junit.platform:junit-platform-launcher:1.4.2'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'

    // ANTLR v4
    antlr 'org.antlr:antlr4:4.5.3'
}

// Tasks
run {
    mainClassName = 'com.github.lessjava.LJCompiler'

    def defaultTestfile = 'tests/fact.lj'
    args = (project.hasProperty('testfile')) ? [testfile] : [defaultTestfile]
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.lessjava.LJCompiler'
    }
}

generateGrammarSource {
    arguments += ['-package', 'com.github.lessjava.generated']
    outputDirectory = file('build/generated-src/antlr/main/com/github/lessjava/generated')
}

clean.doLast {
    file('generated').deleteDir()
}

task fatJar(type:Jar) {
    group 'LessJava'
    description 'Assemble Fat JAR'

    manifest {
        attributes 'Main-Class': 'com.github.lessjava.LJCompiler'
    }
    baseName = 'lj-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyWrappers (type:Copy, dependsOn:':ljwrappers:classes') {
    group 'LessJava'
    description 'Copy compiled source from wrappers subproject'

    from files(project('ljwrappers').sourceSets.main.runtimeClasspath)
    into 'generated/'
}

run.dependsOn(copyWrappers)
