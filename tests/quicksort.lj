partition(A, lo, hi) {
    pivot = A[hi]
    i = lo - 1

    for (j: lo -> hi) {
        if (A[j] < pivot) {
            i = i + 1

            temp = A[i]
            A.set(i, A[j])
            A.set(j, temp)
        }
    }

    temp = A[i + 1]
    A.set(i + 1, A[hi])
    A.set(hi, temp)

    return i + 1
}

sort(A, lo, hi) {
    if (lo < hi) {
        p = partition(A, lo, hi)
        sort(A, lo, p - 1)
        sort(A, p + 1, hi)
    }
}

quicksort(A) {
    sort(A, 0, A.size()-1)
    return A
}

test quicksort([1, 2, 3, 4, 5])             == [1, 2, 3, 4, 5]
test quicksort([5, 4, 3, 2, 1])             == [1, 2, 3, 4, 5]
test quicksort([1, 5, 4, 2, 3])             == [1, 2, 3, 4, 5]
test quicksort([-100, 27, 9999, -9999, 11]) == [-9999, -100, 11, 27, 9999]
